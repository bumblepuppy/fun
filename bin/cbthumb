#!/usr/bin/perl

# vim: filetype=perl5
#
# needs unrar-nonfree
# needs unzip
# needs imagemagic "convert" utility
# needs gs (ghostscript)
#

use feature qw(say);
use warnings;
use strict;
use autodie;
use File::Basename qw(basename);
use URI::Encode;
use FindBin;
use lib "$FindBin::Bin/../lib/perl";
use Text::Wrapper;

sub make_thumbnail_from_first_page { 
    my ($archive_file, $first_page) = @_;
    my $thumbnail_name = "$archive_file.jpg";
    system("convert", $first_page, "-resize", "400x615", $thumbnail_name);
    unlink($first_page);
}

my $wrapper = Text::Wrapper->new(columns => 36);

sub make_default_thumbnail {
    my $archive_file = shift;
    my $label = $wrapper->wrap($archive_file);
    system ("convert",
            "-background", "lightblue",
            "-fill", "blue",
            "-font", "FreeSans",
            "-pointsize","24",
            "-gravity", "center",
            "-size", "400x615",
            "label:$label",
            "$archive_file.jpg"
        );
}

opendir (my $dir_handle,".") || die "can't opendir '.' ";
my @cbr_files = grep { /\.cbr$/i && -f } readdir($dir_handle);
rewinddir($dir_handle);
my @cbz_files = grep { /\.cbz$/i && -f } readdir($dir_handle);
rewinddir($dir_handle);
my @pdf_files = grep { /\.pdf$/i && -f } readdir($dir_handle);

for (@cbr_files) {
    my $rar_file = $_;
    next if -f "$rar_file.jpg";
    my $quoted_rar_file = quotemeta($rar_file);
    my $first_page;
    my $lines;
    if (! open($lines, "unrar -t $quoted_rar_file |" )) {
        make_default_thumbnail($rar_file);
        next;
    }
    my @jpgs;
    while (<$lines>) {
 	chomp;
	next unless /\.jpg$/i;
	next unless /\d\d/;
	push @jpgs, $_;
    }
    @jpgs = sort @jpgs;
    $first_page = $jpgs[0];
    $first_page =~ s/^\s+//;
    say "first page:",$first_page;
    if ( system("unrar", $rar_file, $first_page) ) {
        make_default_thumbnail($rar_file);
        next;
    }
    make_thumbnail_from_first_page($rar_file,$first_page);
    say "rarfile:",$rar_file;
}

for (@cbz_files) {
    my $zip_file = $_;
    next if -f "$zip_file.jpg";
    my $quoted_zip_file = quotemeta($zip_file);
    say "zip_file:",$zip_file;
    say "quoted:",$quoted_zip_file;
    my $first_page;
    open(my $lines, "unzip -l $quoted_zip_file |") || die "can't open quoted zip file: $!";
    my @jpgs;
    while (<$lines>) {
	my $path;
	chomp;
	next unless /\.jpg$/i;
	next unless /\s+\d+\s+\d{4}\-\d\d\-\d\d\s+\d\d\:\d\d\s+(\S+.*)$/;
	$path = $1;
	push @jpgs, $path;
    }
    @jpgs = sort @jpgs;
    $first_page = $jpgs[0];
    $first_page =~ s/^\s+//;
    say "first page:",$first_page;
    system("unzip","-u", $zip_file, $first_page);
    make_thumbnail_from_first_page($zip_file,$first_page);
}

for (@pdf_files) {
    next if -f "$_.jpg";
    system("gs",
	   "-dBATCH",
	   "-dNOPAUSE",
	   "-dSAFER",
	   "-dFirstPage=1",
	   "-dLastPage=1",
	   "-sDEVICE=jpeg",
	   "-dJPEGQ=30",
	   "-r400x615",
	   "-sOutputFile=$_.jpg",
	   $_
	);
}

opendir ($dir_handle,".") || die "can't opendir '.' ";
@cbr_files = grep { /\.cbr$/i && -f } readdir($dir_handle);
rewinddir($dir_handle);
@cbz_files = grep { /\.cbz$/i && -f } readdir($dir_handle);
rewinddir($dir_handle);
@pdf_files = grep { /\.pdf$/i && -f } readdir($dir_handle);


open my $index_file, ">", "index.html";

select $index_file;
say "<html>";
say "<head>";
say "     <title>comic index</title>";
say "     <style>";
say "         .directory {";
say "           display: grid;";
say "           grid-template-columns: repeat(auto-fit, minmax(400px, 1fr));";
say "           grid-gap: 32px;";
say "           grid-auto-flow: dense;";
say "         }";
say "         ";
say "         /* Single column display for phones */";
say "        \@media (max-width: 459px) {";
say "           .directory {";
say "             display: flex;";
say "             flex-direction: column;";
say "           }";
say "         }";
say "    </style>";
say "</head>";
say "<body>";
say "    <div class=\"directory\">";

for (@cbr_files, @cbz_files, @pdf_files) {
    my $archive = URI::Encode::uri_encode($_);
    say "        <article class=\"article\">";
    say "            <a href=\"$archive\"><img src=\"$archive.jpg\" height=\"615\" width=\"400\"></a>";
    say "        </article>";
}

say "    </div>";
say "</body>";
say "</html>";
close $index_file;    
    
